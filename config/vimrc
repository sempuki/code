" Base .vimrc -- Ryan McDougall

colorscheme slate
set termguicolors " Always use a 265 color terminal.

" Set up vundle
filetype off
set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin()
Plugin 'VundleVim/Vundle.vim'
Plugin 'FSwitch'
Plugin 'gtags.vim'
Plugin 'fugitive.vim'
Plugin 'OmniCppComplete'
Plugin 'bfrg/vim-cpp-modern'
Plugin 'rhysd/vim-clang-format'
Plugin 'vim-syntastic/syntastic'
Plugin 'easymotion/vim-easymotion'
Plugin 'haya14busa/incsearch.vim'
Plugin 'haya14busa/incsearch-fuzzy.vim'
Plugin 'haya14busa/incsearch-easymotion.vim'
Plugin 'editorconfig/editorconfig-vim'
Plugin 'skywind3000/asyncrun.vim'
Plugin 'mileszs/ack.vim'
Plugin 'matchit.zip'
call vundle#end()
" :PluginList           - list configured plugins
" :PluginInstall(!)     - install (update) plugins
" :PluginSearch(!) foo  - search (or refresh cache first) for foo
" :PluginClean(!)       - confirm (or auto-approve) removal of unused plugins

filetype plugin indent on   " autoguess by file extension
set nocompatible            " use VIM settings
set autowriteall            " write buffer to file when switching
set undofile                " persistent undo
set tabstop=2               " number of spaces in a tab
set softtabstop=2           " number of spaces in a soft tab
set shiftwidth=2            " used in shifting or C indenting
set expandtab               " insert spaces for tabs
set smarttab                " prefer shiftwidth
set cindent                 " C indenting
set cinoptions=:0,g0,N-s    " minimize switch, class, ns, indenting
set ruler                   " show the cursor
set number                  " print line numbers
set incsearch               " incremental search
set hlsearch                " highlight search matches
set wildmenu                " print menu completions
set wildmode=longest,full   " complete longest substring
set scrolloff=5             " keep lines of context when scrolling
set matchpairs+=<:>         " match angle brackets
set guioptions-=tT          " turn off annoying toolbars
set noerrorbells            " turn off annoying bells
set vb t_vb=                " turn off annoying blinking
let mapleader=","           " comma is more convenient

" Try not to clutter source directories
if isdirectory($HOME.'/.vim/swap') == 0
  :silent !mkdir -p ~/.vim/swap >/dev/null 2>&1
endif
if isdirectory($HOME.'/.vim/undo') == 0
  :silent !mkdir -p ~/.vim/undo > /dev/null 2>&1
endif
set directory=~/.vim/swap//,.,~/tmp//,/var/tmp//
set undodir=~/.vim/undo//,.,~/tmp//,/var/tmp//

" Syntax highlighting always on
if !exists ("syntax_on")
  syntax on
endif

" Save buffer
noremap <silent> <Leader>w <Esc><C-c>:w<CR>

" Cancel highlighting
nnoremap <silent> <C-c> <C-c>:nohl<CR>

" Ergonomic escape
noremap <C-e> <Esc>
noremap! <C-e> <C-c>

" Ergonomic centering
nnoremap <Home> zz

" Additional scrolling
nnoremap <Backspace> <PageUp>
nnoremap <Space> <PageDown>

" Yank/Put to/from system clipboard
vnoremap <Leader>y "+y
vnoremap <Leader>p "+p
nnoremap <Leader>y "+y
nnoremap <Leader>p "+p

" Buffer/Window management
nnoremap <silent> <C-n> <C-W>w
nnoremap <silent> <C-p> <C-W>W
nnoremap <silent> <C-x> :bdelete!<CR>
nnoremap <Leader>e :edit 
nnoremap <Leader>b :buffer 
nnoremap <Leader>B :buffer! # \| bdelete! #<CR>
nnoremap ]b :bnext<CR>
nnoremap [b :bprevious<CR>

" Tab management
nnoremap <C-t><C-t> :tabnext<CR>
nnoremap ]t :tabnext<CR>
nnoremap [t :tabprevious<CR>

" Quickfix window
nnoremap <Leader>q :copen<CR>
nnoremap <Leader>Q :cclose<CR>
nnoremap ]q :cnext<CR>
nnoremap [q :cprevious<CR>

" Location window
nnoremap <Leader>l :lopen<CR>
nnoremap <Leader>L :lclose<CR>
nnoremap ]l :lnext<CR>
nnoremap [l :lprevious<CR>

" Insert newline
nnoremap <C-j> O<Esc>j

" Toggle .{c|cpp}/.{h|hpp}
nnoremap <silent> <C-a> :FSHere<CR>

" Jump to BUILD
nnoremap <silent> <Leader>bb :edit %:h/BUILD<CR>

" Jump to test
nnoremap <silent> <Leader>uu :edit %<_test.cpp<CR>

" Ctag searching
nnoremap <Leader>t :tjump<Space>

" Gtag searching
nnoremap <Leader>r :Gtags -r <C-r><C-w><CR>
nnoremap <Leader>R :Gtags -s <C-r><C-w><CR>
nnoremap <Leader>f :Gtags -f %<CR>
nnoremap <Leader>g :Gtags -g 

" Search+replace word under cursor
nnoremap <Leader>s :,$s/\<<C-r><C-w>\>//gc<Left><Left><Left>

" Grep word under cursor
nnoremap <C-g> :Ack <C-R><C-W> %:h

" Make local file with quickfix integration
" may require manual let $CXXFLAGS='--std=c++11'
nnoremap <Leader><Leader>m :make %< \|cwindow<CR>
nnoremap <Leader><Leader>r :!./%<<CR>

" Async compile in current directory
nnoremap <Leader><Leader>cc :AsyncRun
      \ c++ -Wall -g ${CXXFLAGS} "$(VIM_FILEPATH)" -o "$(VIM_FILENOEXT)"
      \ && lldb -b -o run -k bt ./"$(VIM_FILENOEXT)"<CR>

" vim-cpp-modern Syntax highlighting
let g:cpp_attributes_highlight = 1  " Enable highlighting of C++11 attributes
let g:cpp_member_highlight = 1      " Highlight struct/class member variables

" Automatically open quickfix window and set to 6 lines height
let g:asyncrun_open = 10

" Auto Clang Format
"let g:clang_format#command = 'clang-format-3.6'
let g:clang_format#detect_style_files = 1 " use .clang-format
let g:clang_format#auto_format = 0        " format on write (default to off)
let g:clang_format#auto_formatexpr = 1    " format on gq

fun! ClangFormatPerBufferSetup()
  augroup ClangFormatSetup
    autocmd!

    " Format on write
    :ClangFormatAutoEnable

    " Format on request
    nnoremap = gq
    nnoremap == gqq
    vnoremap = gq

    " Automatic cleanup
    autocmd BufLeave * :ClangFormatAutoDisable
    autocmd BufLeave * silent! nunmap =
    autocmd BufLeave * silent! nunmap ==
    autocmd BufLeave * silent! vunmap =
    autocmd BufLeave * autocmd! ClangFormatSetup
  augroup END
endfun

" Syntastic
let g:syntastic_python_checkers = ['python']
let g:syntastic_cpp_checkers = ['clang_tidy']
let g:syntastic_cpp_clang_tidy_args = '-extra-arg=--std=c++14'
let g:syntastic_always_populate_loc_list = 1
let g:syntastic_auto_loc_list = 2 " close location-list when empty

" FSwitch
let g:fsnonewfiles = 1
augroup FSwitchExtensions
  autocmd!
  autocmd BufEnter *.hh let b:fswitchdst = 'cc'
  autocmd BufEnter *.hh let b:fswitchlocs = 'reg:/include/src/,reg:/include.*/src/,../src'
  autocmd BufEnter *.cc let b:fswitchdst = 'hh,h'
  autocmd BufEnter *.cc let b:fswitchlocs = 'reg:/src/include/,reg:|src|include/**|,../include'
  autocmd BufEnter *.hpp let b:fswitchdst = 'cpp'
  autocmd BufEnter *.hpp let b:fswitchlocs = 'reg:/include/src/,reg:/include.*/src/,../src'
  autocmd BufEnter *.cpp let b:fswitchdst = 'hpp'
  autocmd BufEnter *.cpp let b:fswitchlocs = 'reg:/src/include/,reg:|src|include/**|,../include'
augroup END

" Configure using FileType (instead of extension; FileType < BufEnter/Leave)
autocmd BufEnter * call ConfigureBufferForFileType()
fun! ConfigureBufferForFileType()
  if (&filetype == 'cpp' || &filetype == 'c')
    call ClangFormatPerBufferSetup()
    " Patch categorization error in vim-cpp-modern
    syntax keyword Constant nullptr
    " Add work-specific types
    syntax keyword Type Status Result StatusResult Out InOut PristineOut Optional Variant
    " Add namespace highlighting
    syntax match Special '\<\w\+::'he=e-2
  elseif (&filetype == 'proto')
    call ClangFormatPerBufferSetup()
  elseif (&filetype == 'python')
    " PEP 8 Style
    set tabstop=8 shiftwidth=4 softtabstop=4
  elseif (&filetype == 'java')
    " Eclipse Style
    set tabstop=4 shiftwidth=4 softtabstop=4
  elseif (&filetype == 'javascript')
    " JavaScript
    set tabstop=2 shiftwidth=2 softtabstop=2
  endif
endfun

" Generate local C++ tags file
nnoremap <silent> <Leader>G+ :!ctags -R --languages=C,C++ --c++-kinds=+p --fields-c++=+{properties} --fields=+aiS
      \ -f cpp.tags<CR>
set tags+=./cpp.tags,cpp.tags,

" Generate local C# tags file
nnoremap <silent> <Leader>G# :!ctags -R --languages=C\# --fields=+aiS
      \ -f cs.tags<CR>
set tags+=./cs.tags,cs.tags

" Generate local Java tags file
nnoremap <silent> <Leader>Gj :!ctags -R --languages=Java --fields=+aiS
      \ -f java.tags<CR>
set tags+=./java.tags,java.tags

" Generate local Python tags file
nnoremap <silent> <Leader>Gpy :!ctags -R --languages=Python --python-kinds=-i --fields=+aiS
      \ -f py.tags<CR>
set tags+=./py.tags,py.tags

" Generate local JavaScript tags file
"nnoremap <silent> <Leader>Gjs :!ctags -R --languages=JavaScript --sort=yes -f js.tags<CR>
nnoremap <silent> <Leader>Gjs :!find .
      \ -type f -name '*.js'
      \ -not -path '*/.*'
      \ -not -path '*/bin/*'
      \ -not -path '*/build/*'
      \ -not -path '*/lib/*'
      \ -not -path '*/log/*'
      \ -not -path '*/html/*'
      \ -not -path '*/bazel-*'
      \ -not -path '*/experimental/*'
      \ -not -path '*/node_modules/*'
      \ -exec jsctags {} -f \; \| sed '/^$/d' \| sort > js.tags<CR>
set tags+=./js.tags,js.tags

" Generate local protobuf tags file
nnoremap <silent> <Leader>Gpb :!ctags -R --languages=protobuf --extras=+fq
      \ -f protobuf.tags<CR>
set tags+=./protobuf.tags,protobuf.tags

" Generate /usr/src C++ tags files
nnoremap <silent> <Leader>Gs :!ctags -R --languages=C,C++ --c++-kinds=+p --fields=+aiS --extras=+fq
      \ -f source.tags /usr/src<CR>
set tags+=./source.tags,source.tags

nnoremap <silent> <Leader>Gg :!find .
      \ \(
      \ -name '*.cpp' -or
      \ -name '*.cxx' -or
      \ -name '*.cc' -or
      \ -name '*.c' -or
      \ -name '*.hpp' -or
      \ -name '*.hxx' -or
      \ -name '*.hh' -or
      \ -name '*.h' -or
      \ -name '*.ipp' -or
      \ -name '*.tpp' \)
      \ -not -path '*/.*'
      \ -not -path '*/bin/*'
      \ -not -path '*/build/*'
      \ -not -path '*/lib/*'
      \ -not -path '*/log/*'
      \ -not -path '*/html/*'
      \ -not -path '*/bazel-*'
      \ -not -path '*/experimental/*'
      \ -not -path '*/ThirdParty/*'
      \ -not -path '*/node_modules/*' \|
      \ env GTAGSFORCECPP=1 gtags -f-
      \<CR>

" C++ OmniCppComplete
set completeopt=menu,longest
let OmniCpp_ShowPrototypeInAbbr = 1 "show function parameters
let OmniCpp_DefaultNamespaces = ["std", "_GLIBCXX_STD"]
imap <C-O> <C-X><C-O>

" Avoid searching boost headers on complete
set include=^\\s*#\\s*include.*\\(boost\\)\\@!

" Hex editing
nnoremap <Leader>h :%!xxd -g1<CR>
nnoremap <Leader>H :%!xxd -r<CR>:set bin<CR>:write<CR>:set nobin<CR>

" Text editing
nnoremap <Leader>k :setlocal spell<CR>
nnoremap <Leader>K :setlocal nospell<CR>

" Windows GUI tweaks
if has("gui_win32")
  set guifont=Consolas:h10:cANSI
  set shellslash "unify shell path seperator handling (eg. Cygwin)
endif

" Neovim integration
if has('nvim')
  " Terminal history
  set scrollback=100000

  " Neovim terminal bindings
  nnoremap <C-\>t :set nonumber <bar> :term<CR>
  nnoremap <C-\>s :set nonumber <bar> :split +:term<CR>
  nnoremap <C-\>v :set nonumber <bar> :vsplit +:term<CR>
  tnoremap <Esc><Esc> <C-\><C-n>

  " Ergonomic versions
  nnoremap <C-\><C-t> :set nonumber <bar> :term<CR>
  nnoremap <C-\><C-s> :set nonumber <bar> :split +:term<CR>
  nnoremap <C-\><C-v> :set nonumber <bar> :vsplit +:term<CR>
  tnoremap <C-e><C-e> <C-\><C-n>

  " Fast window switching
  tnoremap <C-n> <C-\><C-n><C-w>wi
  tnoremap <C-p> <C-\><C-n><C-w>Wi
 
  " Fast tab switching
  tnoremap <C-t> <C-\><C-n>:tabnext<CR>

  " Always start on insert for terminal
  ":au BufEnter * if &buftype == 'terminal' | :startinsert | endif
endif

" Replace default f-motions
map f <Plug>(easymotion-fl)
map F <Plug>(easymotion-Fl)
map ; <Plug>(easymotion-repeat)

" Jump to line/word/character; effectively shortcuts for below
map <Leader><Leader>l <Plug>(easymotion-overwin-line)
map <Leader><Leader>w <Plug>(easymotion-overwin-w)
map <Leader><Leader>f <Plug>(easymotion-overwin-f)

" incsearch.vim x fuzzy x vim-easymotion
function! s:config_easyfuzzymotion(...) abort
  return extend(copy({
        \   'converters': [incsearch#config#fuzzyword#converter()],
        \   'modules': [incsearch#config#easymotion#module({'overwin': 1})],
        \   'keymap': {"\<CR>": '<Over>(easymotion)'},
        \   'is_expr': 0,
        \   'is_stay': 0
        \ }), get(a:, 1, {}))
endfunction
noremap <silent><expr> <Leader><Leader>/ incsearch#go(<SID>config_easyfuzzymotion())
